'
' BASE64 ﾃﾞｺｰﾄﾞ ﾃｽﾄ
'
BASE64TABLE$="ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/="

@MAIN
? "*"*19
? " BASE64 ﾃﾞｺｰﾄﾞ ﾃｽﾄ"
? "*"*19
? ""
B64$="ut3Gwdw="
? B64$
? " ↓"
GOSUB @BASE64DEC
? TEXT$
GOSUB @STR2HEXSTR
? HEXSTR$
? ""
? "ﾎﾞﾀﾝ ｦ ｵｼﾃ ｸﾀﾞｻｲ..."
@LOOP
WAIT 1
IF BUTTON()==0 THEN @LOOP
END

'===============
' BASE64 ﾃﾞｺｰﾄﾞ
'===============
@BASE64DEC
' INPUT : B64$
' OUTPUT: TEXT$
TEXT$=""
_C0=0:_C1=0:_C2=0:_C3=0
_B0=0:_B1=0:_B2=0
_T$="":_L=LEN(B64$)
IF (_L%4)!=0 THEN @BASE64DECBRK ' ﾅｶﾞｻ ｶﾞ ｵｶｼｲ
_L=FLOOR(_L/4)-1:_I=0
@BASE64DECLOOP
 IF _I>_L THEN @BASE64DECBRK
 _T$=MID$(B64$,(_I*4)+0,1):_C0=INSTR(BASE64TABLE$,_T$):IF _C0<0 THEN @BASE64DECBRK
 _T$=MID$(B64$,(_I*4)+1,1):_C1=INSTR(BASE64TABLE$,_T$):IF _C1<0 THEN @BASE64DECBRK
 _T$=MID$(B64$,(_I*4)+2,1):_C2=INSTR(BASE64TABLE$,_T$):IF _C2<0 THEN @BASE64DECBRK
 _T$=MID$(B64$,(_I*4)+3,1):_C3=INSTR(BASE64TABLE$,_T$):IF _C3<0 THEN @BASE64DECBRK
 
 _B0=((FLOOR(_C0*POW(2, 2)) AND &HFC) OR (FLOOR(_C1/(POW(2, 4))) AND &H03))
 TEXT$=TEXT$+CHR$(_B0)

 IF _C2==64 THEN @BASE64DECBRK
 _B1=((FLOOR(_C1*POW(2, 4)) AND &HFC) OR (FLOOR(_C2/(POW(2, 2))) AND &H0F))
 TEXT$=TEXT$+CHR$(_B1)
 
 IF _C3==64 THEN @BASE64DECBRK
 _B2=((FLOOR(_C2*POW(2, 6)) AND &HC0) OR (_C3 AND &H3F))
 TEXT$=TEXT$+CHR$(_B2)
 _I=_I+1
 GOTO @BASE64DECLOOP
@BASE64DECBRK
 RETURN

'===============
@STR2HEXSTR
' INPUT : TEXT$
' OUTPUT: HEXSTR$
 HEXSTR$="":_L=LEN(TEXT$)-1
 FOR _I=0 TO _L
  HEXSTR$=HEXSTR$+HEX$(ASC(MID$(TEXT$,_I,1)),2)
 NEXT
 RETURN

